task dependencyReport {
    doLast {
        def file = new File("project-dependencies.dot")
        file.delete()
        file << "digraph {\n"
        file << "splines=ortho\n"
        rootProject.childProjects.each { item ->
            def from = item.value
            from.configurations.compile.dependencies
                    .matching { it in ProjectDependency }
                    .each { to -> file << ("\"${from.name}\" -> \"${to.name}\"\n")}
        }
        file << "}\n"
    }
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'scala'

    ext {
        ////////////////////////////////
        // Commonly used dependencies //
        ////////////////////////////////

        mkDepGen = { genOne -> { String... names -> names.collect(genOne) } }
        finagle = mkDepGen { "com.twitter:finagle-${it}_${scalaInterfaceVersion}:6.26.0" }
        util = mkDepGen { "com.twitter:util-${it}_${scalaInterfaceVersion}:6.25.0" }
        scrooge = mkDepGen { "com.twitter:scrooge-${it}_${scalaInterfaceVersion}:3.19.0" }
        algebird = mkDepGen { "com.twitter:algebird-${it}_${scalaInterfaceVersion}:0.10.2" }
        zipkin = mkDepGen { project(":zipkin-${it}") }

        hadoop = mkDepGen { "org.apache.hadoop:hadoop-${it}:2.4.0" }
        hadoopTest = { String... names -> hadoop(*names) + hadoop(*names).collect{"${it}:tests"} }

        twitterZookeeperVersions = [
                'candidate': "0.0.75",
                'group'    : "0.0.81",
                'client'   : "0.0.70",
                'server-set': "1.0.103"
        ]
        zk = mkDepGen { "com.twitter.common.zookeeper:${it}:${twitterZookeeperVersions[it]}" }

        twitterServer = "com.twitter:twitter-server_${scalaInterfaceVersion}:1.11.0"
        junit = 'junit:junit:4.12'
        slf4jLog4j12 = "org.slf4j:slf4j-log4j12:1.6.4"  // Dont forget to use for configuration 'runtime' only
        ostrich = "com.twitter:ostrich_${scalaInterfaceVersion}:9.9.0"
        griddle = 'com.yodle.griddle:griddle:1.7'
    }

    //////////////////////////
    // Common configuration //
    //////////////////////////

    // SBT migration note: start of zipkinSettings
    group "io.zipkin"
    version zipkinVersion

    repositories {
        jcenter()
        maven { url 'https://maven.twttr.com/' }
        maven { url 'http://repo.typesafe.com/typesafe/releases/' }
        // For zipkin-cassandra deps and com.twitter:kafka
        maven { url 'http://conjars.org/repo' }
        // For storm-kafka
        maven { url 'https://clojars.org/repo' }
    }

    dependencies {
        compile "org.scala-lang:scala-library:${scalaVersion}",
                // Was dependencyOverrides in SBT, will probably cause issues
                'org.apache.zookeeper:zookeeper:3.4.6', // internal twitter + kafka + curator
                'org.slf4j:slf4j-api:1.6.4' // libthrift 0.5 otherwise pins 1.5.x
                // EOF dependencyOverrides

        runtime "org.scala-lang:scala-library:${scalaVersion}"

        testCompile junit,
                'org.mockito:mockito-all:1.10.19',
                "org.scalatest:scalatest_${scalaInterfaceVersion}:2.2.5"
    }
    // SBT migration note: end of zipkinSettings
}
